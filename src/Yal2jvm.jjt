options
{
  static = true;
  //LOOKAHEAD= 1;
}

PARSER_BEGIN(Yal2jvm)
import java.io.*;
import java.nio.charset.Charset;

public class Yal2jvm
{
  public static final Logger LOGGER = new Logger(10);

  public static void main(String args []) throws IOException{
    if(args.length == 1) {
		File initialFile = new File(args[0]);
    	InputStream is = new FileInputStream(initialFile);
     	new Yal2jvm(is);
		}
      
	else {
    System.out.println("Reading from standard input...");
    new Yal2jvm(System.in); }
  
    try
    {
      SimpleNode n = Yal2jvm.Start();
    }
   
	catch (Exception e)
    {
      System.out.println("Oops.");
      System.out.println(e.getMessage());
    }
  }
}

PARSER_END(Yal2jvm)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| < "//" (~[ "\n", "\r" ])*
    (
      "\n"
    | "\r"
    | "\r\n"
    ) >
| < "/*" (~[ "*" ])* "*"
    (
      "*"
    | ~[ "*", "/" ] (~[ "*" ])* "*"
    )*
    "/" >
}

TOKEN : /* LITERALS */
{
  < RELA_OP :
    ">"
  | "<"
  | "<="
  | ">="
  | "=="
  | "!=" >
| < ADDSUB_OP :
    "+"
  | "-" >
| < ARITH_OP :
    "*"
  | "/"
  | "<<"
  | ">>"
  | ">>>" >
| < BITWISE_OP :
    "&"
  | "|"
  | "^" >
| < NOT_OP : "!" >
| < WHILE : "while" >
| < IF : "if" >
| < ELSE : "else" >
| < ASSIGN : "=" >
| < ASPA : "\"" >
| < LPAR : "(" >
| < RPAR : ")" >
| < VIRG : "," >
| < PVIRG : ";" >
| < LCHAVETA : "{" >
| < RCHAVETA : "}" >
| < FUNCTION : "function" >
| < MODULE : "module" >
| < SIZE : "size" >
}

TOKEN : /* IDENTIFIERS */
{
  < INTEGER : (< DIGIT >)+ >
| < ID :
    < LETTER >
    (
      < LETTER >
    | < DIGIT >
    )* >
| < #LETTER : [ "$", "A"-"Z", "_", "a"-"z" ] >
| < #DIGIT : [ "0"-"9" ] >
| < STRING : "\"" ([ "a"-"z", "A"-"Z", "0"-"9", ":", " ", "=" ])+ "\"" >
}

SimpleNode Start() :
{}
{
  (
    < MODULE > < ID > < LCHAVETA >
    (
      Ex_Declaration()
    )*
    (
      Function()
    )*
    < RCHAVETA >
  )
  {
    return jjtThis;
  }
}

void Ex_Declaration() :
{}
{
  try
  {
    Declaration()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(PVIRG);
  }
}

void Declaration() :
{}
{
  (
    Element()
  )
  (
    < ASSIGN >
    (
      (
        "[" ArraySize() "]"
      )
    | (< ADDSUB_OP >)? < INTEGER >
    )
  )?
  < PVIRG > 
}

void Ex_Function() :
{}
{
  try
  {
    Function()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(RCHAVETA);
  }
}

void Function() :
{}
{
  < FUNCTION > 
  < ID >
  (
    ("[" "]")? //Element 
    < ASSIGN > 
    < ID >
  )?
  < LPAR >
  (
    Varlist()
  )?
  < RPAR > 
  < LCHAVETA > Stmtlst() < RCHAVETA >
}

void Varlist() :
{}
{
  Element()
  (
    < VIRG > 
    Element()
  )*
}

void Element() : //Array or scalar element
{}
{
  < ID > ("[" "]")?
}

void Stmtlst() :
{}
{
  (
    Stmt()
  )*
}

void Stmt() :
{}
{
  While()
| If()
  // | Assign() | Call() <PVIRG>
| StmtCallOrAssign()
}

void Ex_StmtCallOrAssign() :
{}
{
  try
  {
    StmtCallOrAssign()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(PVIRG);
  }
}

void StmtCallOrAssign() :
{}
{
  < ID >
  (
    
    (
      "[" Index() "]"
    )?
    < ASSIGN > Rhs() < PVIRG > //Assign
  | 
    "."
    (
      (
        < SIZE > < ASSIGN > Rhs() < PVIRG > //Assign
      | 
        < ID > < LPAR >
        (
          ArgumentList()
        )?
        < RPAR > < PVIRG > //Call
      )
    )
  | 
    < LPAR >
    (
      ArgumentList()
    )?
    < RPAR > < PVIRG >
  ) //Call
}

void Ex_Assign() :
{}
{
  try
  {
    Assign()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(PVIRG);
  }
}


void Assign() :
{}
{
  Access() < ASSIGN > Rhs() < PVIRG >
}

void Rhs() :
{}
{
  (
    Term()
    (
      (
        < ARITH_OP >
      | < BITWISE_OP >
      | < ADDSUB_OP >
      )
      Term()
    )?
  )
| "[" ArraySize() "]"
}

void ArraySize() :
{}
{
  ScalarAccess()
| < INTEGER >
}

void Term() :
{}
{
  (< ADDSUB_OP >)?
  (
    < INTEGER >
    //| Call() | Access()
  | CallOrAccess()
  )
}

void CallOrAccess() :
{}
{
  < ID >
  (
    < LPAR >
    (
      ArgumentList()
    )?
    < RPAR >
  | "."
    (
      < ID > < LPAR >
      (
        ArgumentList()
      )?
      < RPAR >
    | < SIZE >
    )
  | "[" Index() "]"
  )?
}

void Ex_Exprtest() :
{}
{
  try
  {
    Exprtest()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(RPAR);
  }
}

void Exprtest() :
{}
{
  < LPAR > Access() < RELA_OP > Rhs() < RPAR >
}

void Ex_While() :
{}
{
  try
  {
    While()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(RCHAVETA);
  }
}


void While() :
{}
{
  < WHILE > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
}

void Ex_If() :
{}
{
  try
  {
    If()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(RCHAVETA);
  }
}

void If() :
{}
{
  < IF > Exprtest() < LCHAVETA > Stmtlst() < RCHAVETA >
  (
    < ELSE > < LCHAVETA > 
    Stmtlst() < RCHAVETA >
  )?
}

void Ex_Call() :
{}
{
  try
  {
    Call()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(RPAR);
  }
}

void Call() :
{}
{
  < ID > ("." < ID >)? < LPAR >
  (
    ArgumentList()
  )?
  < RPAR >
}

void ArgumentList() :
{}
{
  Argument()
  (
    < VIRG > Argument()
  )*
}

void Ex_Argument() :
{}
{
  try
  {
    Argument()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(INTEGER);
  }
}

void Argument() :
{}
{
  (
    < ID >
  | < STRING >
  | < INTEGER >
  )
}

void Access() : //Array or scalar access
{}
{
  < ID >
  (
    "." < SIZE >
  | "[" Index() "]"
  )?
}

void ScalarAccess() :
{}
{
  < ID > ("." < SIZE >)?
}

void Ex_Index() :
{}
{
  try
  {
    Index()
  }
  catch (ParseException e)
  {
    LOGGER.error(e);
    error_skipto(INTEGER);
  }
}

void Index() :
{}
{
  < ID >
| < INTEGER >
}

JAVACODE 
void error_skipto(int kind)
{
  ParseException e = generateParseException(); // generate the exception object.
  System.out.println
  (
    e.toString()
  )
  ; // print the error message -- over here errors should get written into log
  Token t;
  do
  {
    t = getNextToken();
  }
  while (t.kind != kind);
  // The above loop consumes tokens all the way up to a token of
  // "kind".  We use a do-while loop rather than a while because the
  // current token is the one immediately before the erroneous token
  // (in our case the token immediately before what should have been
  // "if"/"while".
}